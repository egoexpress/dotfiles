# .zshrc - main ZSH configuration

# path to oh-my-zsh installation
export ZSH=$HOME/.oh-my-zsh

# set custom path for oh-my-zsh
export ZSH_CUSTOM=$HOME/.zsh-custom

# fix LC_CTYPE
export LC_CTYPE="en_US.UTF-8"

# allow to define functions based on an alias
setopt alias_func_def 2>/dev/null

# use hyphen-insensitive completion
HYPHEN_INSENSITIVE="true"

# disable bi-weekly auto-update checks.
DISABLE_AUTO_UPDATE="true"

# display red dots whilst waiting for completion.
COMPLETION_WAITING_DOTS="true"

# oh-my-zsh plugins
plugins=(
  eza
  git
  colored-man
  colorize
  compleat
  history
  mosh
  tmux
  tmuxinator
  pip
  python
  virtualenv
  brew
  osx
  brew-cask
  git-extras
  github
  z
  zsh-syntax-highlighting
  k
  zsh-completions
  docker
  docker-compose
  ubuntu
  colored-man-pages
  command-not-found
  zsh_reload
  alias-finder
  autojump
  fzf
  ripgrep
  zsh-autosuggestions
  ssh-agent
)

. $ZSH/oh-my-zsh.sh >/dev/null 2>&1

# path to dotfiles
export DOTFILES="$HOME/.dotfiles"

# get all zsh .dotfiles
typeset -U config_files
config_files=($DOTFILES/**/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

export PRIVATEFILES="$HOME/.private"

if test -d ${PRIVATEFILES}; then
  typeset -U private_files
  private_files=($PRIVATEFILES/**/*.zsh)

  # load everything but the path and completion files in ~/.private
  for file in ${${private_files:#*/path.zsh}:#*/completion.zsh}
  do
    source $file
  done
fi

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files

# better history
# credits to https://coderwall.com/p/jpj_6q/zsh-better-history-searching-with-arrow-keys
autoload -U up-line-or-beginning-search
autoload -U down-line-or-beginning-search
zle -N up-line-or-beginning-search
zle -N down-line-or-beginning-search
bindkey "^[[A" up-line-or-beginning-search # Up
bindkey "^[[B" down-line-or-beginning-search # Down

# make $PATH entries unique
typeset -aU path

# set dircolors
eval `dircolors ~/.dircolors`

# configuration for hstr (https://github.com/dvorka/hstr)

# ensure history file visibility
export HISTFILE=~/.zsh_history

# get more colors, use custom blacklist, dont confirm history delete
export HH_CONFIG=hicolor,noconfirm,blacklist

# bind hh to Ctrl-r (for Vi mode check doc)
bindkey -s "\C-r" "\eqhh\n"

# enable vi-style editing
# see https://dougblack.io/words/zsh-vi-mode.html
bindkey -v

bindkey '^P' up-history
bindkey '^N' down-history
bindkey '^?' backward-delete-char
bindkey '^h' backward-delete-char
bindkey '^w' backward-kill-word
bindkey '^r' history-incremental-search-backward

function zle-line-init zle-keymap-select {
    VIM_PROMPT="%{$fg_bold[yellow]%} [% NORMAL]%  %{$reset_color%}"
    RPS1="${${KEYMAP/vicmd/$VIM_PROMPT}/(main|viins)/} $EPS1"
    zle reset-prompt
}

zle -N zle-line-init
zle -N zle-keymap-select
export KEYTIMEOUT=1

export TERM=xterm-256color

# load fzf key bindings and completions
source ${DOTFILES}/zsh/fzf.zsh

[[ ! -f ~/.config/broot/launcher/bash/br ]] || source ~/.config/broot/launcher/bash/br

# load starship as prompt theme
eval "$(starship init zsh)"

export NVM_DIR="$HOME/.nvm"
[ -s "$NVM_DIR/nvm.sh" ] && \. "$NVM_DIR/nvm.sh"
[ -s "$NVM_DIR/bash_completion" ] && \. "$NVM_DIR/bash_completion" 

